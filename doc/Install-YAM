;
; $VER: Install-YAM 1.28 (24.4.2013) (c) 2000-2013 by YAM Open Source Team
; Language support added by Sönke Tesch/ATO
;
; $Id$
;
;
; /// Setting up
(set #def_lang_english             $000)
(set #def_lang_german              $001)
(set #def_lang_swedish             $002)
(set #def_lang_czech               $004)
(set #def_lang_italian             $008)
(set #def_lang_french              $010)
(set #def_lang_greek               $020)
(set #def_lang_spanish             $040)
(set #def_lang_turkish             $080)
(set #def_lang_hungarian           $100)
;(set #def_lang_dutch               $200)
;(set #def_lang_polish              $400)
; add more languages here
; (set #def_lang_<language name>     bitmask)
;
; accents of languages should be appended at the end rather than somewhere in the middle,
; because only AmigaOS 4.x can handle them.
(set #def_lang_english_british     $200)
; add more accents of languages here
; (set #def_lang_<language name>     bitmask)
; ///

;----------------------------------------------------------------------------
;/// ***** English
(set #def_cat_lang #def_lang_english)
(set #acknowledgeOS (cat "Please acknowledge the automatic\n"
                         "detection of your operating system:"))
(set #YAM_dir  (cat "Please select a directory for YAM\n"
                    "(NO extra drawer will be created!)"))
(set #lang_catalog (cat "Which catalog file do you want to install?\n"
                        "(English language is built-in)"))
(set #ask_charsets "Do you want to install the character set files required by codesets.library?")
(set #no_way_back_warning (cat "WARNING!!\n"
                               "(if you want to update an existing YAM 2.4 installation)\n"
                               "\n"
                               "The mail naming scheme has been changed completely. When you run YAM 2.8 for the first time all your existing mails will be converted automatically to the new scheme. After that YAM 2.4 will not be able to handle them anymore. So better backup ALL your mails BEFORE running YAM 2.8.\n"
                               "\n"
                               "However, it is absolutely NO problem to install YAM 2.4 and YAM 2.8 in separate directories."))
(set #faq_and_bugtracker (cat "\n"
                              "If you have any questions concerning YAM you can ask these in the YAM forum:\n"
                              "http://forum.yam.ch/\n"
                              "\n"
                              "Frequently asked questions are already answered in the YAM FAQ:\n"
                              "http://faq.yam.ch/\n"
                              "\n"
                              "In case you find a bug in YAM, please, report it in the YAM bugtracker:\n"
                              "http://bugs.yam.ch/"))
(set #done_1   (cat "\n"
                    "YAM has been successfully installed.\n"
                    "\n"
                    "Double click the \"YAM\" icon in the\n"))
(set #done_2   (cat "\n"
                    "directory to taste it.\n"
                    "\n"
                    "Have fun..."))

; ///
; /// ***** Deutsch
(if (= @language "deutsch")
    (
        (set #def_cat_lang #def_lang_german)
        (set #acknowledgeOS (cat "Bitte bestätigen Sie die automatische\n"
                                 "Ermittlung Ihres Betriebssystems:"))
        (set #YAM_dir  (cat "Wählen Sie ein Verzeichnis für YAM\n"
                            "(es wird KEIN eigenes Verzeichnis erstellt!)"))
        (set #lang_catalog (cat "Welche catalog-Datei wollen Sie installieren?\n"
                                "(Englisch ist eingebaut)"))
        (set #ask_charsets "Wollen Sie die von der codesets.library benötigten Zeichensatzdateien installieren?")
        (set #no_way_back_warning (cat "WARNUNG!!\n"
                                       "(falls Sie eine bestehende Installation von YAM 2.4 updaten wollen)\n"
                                       "\n"
                                       "Das Namensformat der EMail-Dateien wurde grundlegend geändert. Wenn Sie YAM 2.8 zum ersten Mal starten werden alle Ihre EMails automatisch auf das neue Namensformat konvertiert. Danach können sie NICHT mehr von YAM 2.4 verarbeitet werden. Machen Sie also besser ein Backup BEVOR Sie YAM 2.8 starten.\n"
                                       "\n"
                                       "Es ist absolut KEIN Problem YAM 2.4 und YAM 2.8 in unterschiedlichen Verzeichnissen zu installieren."))
        (set #faq_and_bugtracker (cat "\n"
                                      "Falls Sie allgemeine Fragen zu YAM haben, können Sie diese im YAM-Forum stellen:\n"
                                      "http://forum.yam.ch/\n"
                                      "\n"
                                      "Oft gestellte Fragen werden in der YAM-FAQ beantwortet:\n"
                                      "http://faq.yam.ch/\n"
                                      "\n"
                                      "Falls Sie einen Fehler in YAM finden sollten, dann melden Sie ihn bitte im YAM-Bugtracker:\n"
                                      "http://bugs.yam.ch/"))
        (set #done_1   (cat "\n"
                            "YAM wurde erfolgreich installiert.\n"
                            "\n"
                            "Klicken Sie auf das \"YAM\"-Icon im Verzeichnis\n"))
        (set #done_2   (cat "\n"
                            "und es kann losgehen.\n"
                            "\n"
                            "Viel Spass!"))
    )
)
; ///
; /// ***** Polish
(if (= @language "polski")
    (
        (set #def_cat_lang #def_lang_english)
        (set #acknowledgeOS (cat "Prosze potwierdzic automatycznie\n"
                                 "wykryty system operacyjny:"))
        (set #YAM_dir  (cat "Prosze wybrac katalog dla programu YAM\n"
                            "(NIE zostanie utworzony dodatkowy podkatalog!)"))
        (set #lang_catalog (cat "Ktore pliki lokalizacyjne chcesz zainstalowac?\n"
                                "(jezyk angielski jest wbudowany)"))
        (set #ask_charsets "Czy chcesz zainstalowac wymagane przez codesets.library pliki zestawow znakow?")
        (set #no_way_back_warning (cat "OSTRZEZENIE!!\n"
                                       "(jesli chcesz uaktualnic istniejaca instalacje programu YAM 2.4)\n"
                                       "\n"
                                       "Zostal calkowicie zmieniony system nazywania plikow wiadomosci. Przy pierwszym uruchomieniu programu YAM 2.8, wszystkie istniejace wiadomosci zostana automatycznie przekonwertowane na nowy format. Po tej czynnosci YAM 2.4 nie bedzie potrafil juz ich obsluzyc. Lepiej wykonaj kopie bezpieczenstwa WSZYSTKICH swoich wiadomosci PRZED uruchomieniem programu YAM 2.8.\n"
                                       "\n"
                                       "Jednakze nie stanowi zadnego problemu zainstalowanie programow YAM 2.4 i 2.8 w osobnych katalogach."))
        (set #faq_and_bugtracker (cat "\n"
                                      "Jesli masz jakies pytania dotyczace programu, mozesz je zadac na forum:\n"
                                      "http://forum.yam.ch/\n"
                                      "\n"
                                      "Najczesciej zadawane pytania sa juz zebrane w FAQ:\n"
                                      "http://faq.yam.ch/\n"
                                      "\n"
                                      "W przypadku znalezienia bledu w programie, prosze dokonac zgloszenia na:\n"
                                      "http://bugs.yam.ch/"))
        (set #done_1   (cat "\n"
                            "Program YAM zostal zainstalowany pomyslnie.\n"
                            "\n"
                            "Dwukliknij na ikonce \"YAM\" w katalogu\n"))
        (set #done_2   (cat "\n"
                            "by go wyprobowac.\n"
                            "\n"
                            "Milej zabawy..."))
    )
)
; ///
; /// ***** Svenska
(if (= @language "svenska")
    (
        (set #def_cat_lang #def_lang_swedish)
        (set #acknowledgeOS (cat "Var god bekräfta den automatiska\n"
                                 "detekteringen av ditt operativsystem:"))
        (set #YAM_dir  (cat "Välj katalog att installera YAM i\n"
                            "(någon underkatalog kommer INTE att skapas!)"))
        (set #lang_catalog (cat "Vilka språkkataloger vill du installera?\n"
                                "(Engelska är inbyggt)"))
        (set #ask_charsets "Vill du installera de teckenset som krävs av codesets.library?")
        (set #no_way_back_warning (cat  "VARNING!!\n"
                                       "(om du vill uppdatera en existerande YAM 2.4 installation)\n"
                                       "\n"
                                       "Namnschemat för mailfilerna har ändrats totalt. När du kör YAM 2.8 för första gången, kommer alla dina nuvarande mailfiler att konverteras automatiskt till det nya schemat. Efter det kommer inte YAM 2.4 att kunna hantera dem längre. Så, backa upp ALLA dina mailfiler FÖRE du kör YAM 2.8.\n"
                                       "\n"
                                       "Men, det är absolut INGA problem att installera YAM 2.4 och YAM 2.8 i separata kataloger."))
        (set #faq_and_bugtracker (cat "\n"
                                      "Om du har några frågor rörande YAM kan du fråga dessa i YAM's forum:\n"
                                      "http://forum.yam.ch/\n"
                                      "\n"
                                      "Frequently asked questions är redan besvarade i YAM's FAQ:\n"
                                      "http://faq.yam.ch/\n"
                                      "\n"
                                      "Om du skulle hitta en bugg i YAM, var god rapportera det i YAM's buggtracker:\n"
                                      "http://bugs.yam.ch/"))
        (set #done_1   (cat "\n"
                            "YAM installerades utan problem\n"
                            "\n"
                            "Klicka på \"YAM\"-ikonen i katalogen\n"))
        (set #done_2   (cat "\n"
                            "för att pröva det.\n"
                            "\n"
                            "Ha det så kul..."))
    )
)
; ///
; /// ***** Français
(if (= @language "français")
    (
        (set #def_cat_lang #def_lang_french)
        (set #acknowledgeOS (cat "Veuillez confirmer la détection\n"
                                 "automatique de votre système d'exploitation :"))
        (set #YAM_dir  (cat "Veuillez sélectionner un répertoire pour YAM\n"
                            "(AUCUN nouveau tiroir ne sera créé !)"))
        (set #lang_catalog (cat "Quel catalogue voulez-vous installer ?\n"
                                "(L'anglais est déjà incorporé)"))
        (set #ask_charsets "Voulez-vous installer les fichiers de jeux de caractères requis par codesets.library ?")
        (set #no_way_back_warning (cat "ATTENTION !!\n"
                                       "(si vous voulez mettre à jour une installation existante de YAM 2.4)\n"
                                       "\n"
                                       "Le schéma de nommage des messages a complètement changé. Lorsque vous lancez YAM 2.8 pour la première fois, tous vos messages existants seront automatiquement convertis. Après cela YAM 2.4 ne pourra plus les gérer. Vous devriez donc faire une sauvegarde de TOUS vos messages AVANT de lancer YAM 2.8.\n"
                                       "\n"
                                       "Cependant, il n'y a absolument AUCUN problème à installer YAM 2.4 et 2.8 dans des répertoires différents."))
        (set #faq_and_bugtracker (cat "\n"
                                      "Si vous avez des questions concernant YAM vous pouvez les poser sur le forum :\n"
                                      "http://forum.yam.ch/\n"
                                      "\n"
                                      "Une Foire Aux Questions déjà répondues se trouve à l'adresse :\n"
                                      "http://faq.yam.ch/\n"
                                      "\n"
                                      "Si vous trouvez un bug dans YAM, veuillez le rapporter dans le suivi d'anomalies :\n"
                                      "http://bugs.yam.ch/"))
        (set #done_1   (cat "\n"
                            "YAM a été installé avec succés.\n"
                            "\n"
                            "Double-cliquez the sur l'icône « YAM » dans\n"))
        (set #done_2   (cat "\n"
                            "le répertoire pour le tester.\n"
                            "\n"
                            "Amusez-vous bien!"))
    )
)
; ///
; /// ***** Czech
(if (= @language "czech")
    (
        (set #def_cat_lang #def_lang_czech)
        (set #YAM_dir  (cat "Vyberte prosím cílový adresáø pro YAM\n"
                            "(Instalátor ¾ádný adresáø nevytvoøí!)"))
        (set #lang_catalog (cat "Které z pøekladù chcete nainstalovat?\n"
                                "(Angliètinu pou¾ívá program internì.)"))
        (set #ask_charsets "Chcete nainstalovat dodateèná kódování pro knihovnu codesets.library?")
        (set #no_way_back_warning (cat "UPOZORNÌNÍ!\n"
                                       "(Platí pro pøípad, ¾e chcete pøejít z verze 2.4.)\n"
                                       "\n"
                                       "Zpùsob pojmenování souborù se zprávami a jejich formát byl v nové verzi úplnì pøepracován. Pøi prvním spu¹tìní budou v¹echny zprávy automaticky pøevedeny do nového systému a pak u¾ je nebude mo¾né pou¾ívat v YAM 2.4! Je¹tì PØED spu¹tìním této nové verze proto dùraznì doporuèujeme zazálohovat V©ECHNY zprávy.\n"
                                       "\n"
                                       "Pozn.: Technicky není ¾ádný problém nainstalovat a pou¾ívat YAM 2.4 i novìj¹í verzi ve dvou rùzných adresáøích."))
        (set #faq_and_bugtracker (cat "\n"
                                      "Máte-li jakékoli dotazy ohlednì programu YAM, zeptejte se na tomto diskusním fóru:\n"
                                      "http://forum.yam.ch/\n"
                                      "\n"
                                      "Nejèastìji kladené otázky a jejich øe¹ení najdete na adrese:\n"
                                      "http://faq.yam.ch/\n"
                                      "\n"
                                      "Pokud najdete chybu, dejte nám prosím vìdìt:\n"
                                      "http://bugs.yam.ch/"))
        (set #done_1   (cat "\n"
                            "Instalace programu YAM byla úspì¹nì dokonèena.\n"
                            "\n"
                            "Nyní vyhledejte ikonu \"YAM\" v adresáøi\n"))
        (set #done_2   (cat "\n"
                            "a dvojím kliknutím program spus»te.\n"
                            "\n"
                            "A» se Vám líbí!"))
    )
)
; ///
; /// ***** Italiano
(if (= @language "italiano")
    (
        (set #def_cat_lang #def_lang_italian)
        (set #acknowledgeOS (cat "Per favore istruisci il sistema ad auto-rilevare\n"
                         "i file necessari per il sistema operativo in uso:"))
        (set #YAM_dir  (cat "Per favore seleziona una directory per YAM\n"
                            "(Non verrà creata nessuna cartella extra!)"))
        (set #lang_catalog (cat "Quale catalogo di lingua si desidera installare?\n"
                                "(La lingua inglese è quella predefinita)"))
        (set #ask_charsets "Si desidera installare i file del set di caratteri richiesti dalla codesets.library?")
        (set #no_way_back_warning (cat "ATTENZIONE!!\n"
                                       "(Nel caso si desideri aggiornare il programma da una precedente installazione di YAM 2.4.)\n"
                                       "\n"
                                       "Lo schema dei nomi relativo ai messaggi di posta è stato completamente modificato, alla prima esecuzione di YAM 2.8 tutti i precedenti messaggi verranno automaticamente convertiti al nuovo schema. Nota che YAM 2.4 non sarà più in grado di accedere a questi messaggi perciò è consigliabile effettuare un backup di TUTTI i vecchi messaggi di posta prima di procedere con questa operazione.\n"
                                       "\n"
                                       "Ricorda che seppur in directory separate, sarà sempre possibile installare due differenti versioni di YAM."))
        (set #faq_and_bugtracker (cat "\n"
                                      "Nel caso si abbiano delle domande concernenti YAM ed il suo sviluppo potete contattarci attraverso il nostro forum all'indirizzo internet:\n"
                                      "http://forum.yam.ch/\n"
                                      "\n"
                                      "Nota che le domande più frequentemente risposte sono disponibili nelle FAQ del sito di YAM all'indirizzo:\n"
                                      "http://faq.yam.ch/\n"
                                      "\n"
                                      "Nel caso si riscontri qualche bug o qualsiasi altro problema su YAM, potete segnalarcelo attraverso il nostro bugtracker all'indirizzo:\n"
                                      "http://bugs.yam.ch/"))
        (set #done_1   (cat "\n"
                            "Congratulazioni, YAM è stato installato con successo!\n"
                            "\n"
                            "Doppio click sull'icona di \"YAM\" nella directory\n"))
        (set #done_2   (cat "\n"
                            "in modo da poter avviare il programma.\n"
                            "\n"
                            "Buon divertimento!"))
    )
)
; ///
;/// ***** Greek
(if (= @language "greek")
    (
        (set #def_cat_lang #def_lang_greek)
        (set #acknowledgeOS (cat "Ðáñáêáëþ áíáãíùñßóôå ôçí áõôüìáôç áíß÷íåõóç\n"
        	                 "ôïõ ëåéôïõñãéêïý óáò óõóôÞìáôïò:"))
        (set #YAM_dir  (cat "Ðáñáêáëþ åðéëÝîôå Ýíá êáôÜëïãï ãéá ôï YAM.\n"
                            "(ÄÅÍ èá äçìéïõñãçèåß åðéðëÝïí öÜêåëïò)"))
        (set #lang_catalog (cat "Ðïéü áñ÷åßï êáôáëüãïõ èÝëåôå íá åãêáôáóôÞóåôå;\n"
                                "(Ç áããëéêÞ ãëþóóá åßíáé åíóùìáôïìÝíç)"))
        (set #ask_charsets "ÈÝëåôå íá åãêáôáóôÞóåôå ôá áñ÷åßá êùäéêïðïßçóçò ÷áñáêôÞñùí ðïõ áðáéôåßôáé áðü ôçí codesets.library;"
        (set #no_way_back_warning (cat "ÐÑÏÓÏ×Ç !!!\n"
                                       "(åÜí èÝëåôå íá áíáâáèìßóåôå ìéá Þäç õðÜñ÷ïõóá åãêáôÜóôáóç ôïõ YAM 2.4)\n"
                                       "\n"
                                       "Ç ìïñöÞ ïíïìÜôùí áëëçëïãñáößáò Ý÷åé áëëÜîåé åíôåëþò. ¼ôáí ôñÝîåôå ôï YAM 2.8 ãéá ðñþôç öïñÜ üëåò ïé õðÜñ÷ïõóåò áëëçëïãñáößåò èá ìåôáôñáðïýí áõôüìáôá óôç íÝá ìïñöÞ. ÌåôÜ áðü áõôü, ôï YAM 2.4 äå èá ìðïñåß íá ôéò ÷åéñéóôåß ðëÝïí. ¸ôóé åßíáé êáëýôåñá íá êÜíåôå áíôßãñáöï áóöáëåßáò óå ¼ËÅÓ ôéò áëëçëïãñáößåò óáò ÐÑÉÍ ôñÝîåôå ôï YAM 2.8.\n"
                                       "\n"
                                       "¼ìùò, äåí õðÜñ÷åé ÊÁÍÅÍÁ ðñüâëçìá íá åãêáôáóôÞóåôå ôï YAM 2.4 êáé ÕAM 2.8 óå äéáöïñåôéêïýò êáôáëüãïõò."))
        (set #faq_and_bugtracker (cat "\n"
                                      "ÅÜí Ý÷åôå ïðïéáäÞðïôå áðïñßá ó÷åôéêÜ ìå ôï YAM ìðïñåßôå íá ôçí áíáöÝñåôå óôá forum ôïõ YAM:\n"
                                      "http://forum.yam.ch/\n"
                                      "\n"
                                      "Óõ÷íÝò åñùôÞóåéò ðïõ Ý÷ïõí Þäç áðáíôçèåß âñßóêïíôáé óôï YAM FAQ:\n"
                                      "http://faq.yam.ch/\n"
                                      "\n"
                                      "Óå ðåñßðôùóç ðïõ âñåßôå êÜðïéï ðñüâëçìá óôï YAM, ðáñáêáëþ, áíáöÝñåôÝ ôï óôï YAM bugtracker:\n"
                                      "http://bugs.yam.ch/"))
        (set #done_1   (cat "\nÔï YAM åãêáôáóôÜèçêå åðéôõ÷þò.\n"
                             "\n"
                            "ÊÜíôå äéðëü-êëßê óôï åéêïíßäéï \"YAM\" óôï\n"))
        (set #done_2   (cat "\nêáôÜëïãï ãéá íá ôï äïêéìÜóåôå.\n"
                            "\n"
                            "ÊáëÞ äéáóêÝäáóç!"))
    )
)
; ///
;/// ***** Español
(if (= @language "español")
    (
        (set #def_cat_lang #def_lang_spanish)
        (set #acknowledgeOS (cat "Por favor confirme la detección\n"
                                 "automática de su sistema operativo:"))
        (set #YAM_dir  (cat "Por favor seleccione un directorio para YAM\n"
                            "(NO se creará un cajón extra!)"))
        (set #lang_catalog (cat "Qué catálogo desea instalar?\n"
                                "(Inglés está por defecto)"))
        (set #no_way_back_warning (cat "ATENCION!!\n"
                                       "(si quiere actualizar la versión existente\n"
                                       "de YAM 2.4)\n"
                                       "\n"
                                       "El esquema de denominación de correo ha cambiado completamente. Cuando ejecute YAM 2.8 por primera vez todos sus correos existentes serán convertidos automáticamente al nuevo esquema. Después de esto YAM 2.4 no se podrá utilizar más. Así que mejor guarde TODOS sus correos ANTES de ejecutar YAM 2.8.\n"
                                       "\n"
                                       "Aunque, NO hay problema en absoluto el instalar YAM 2.4 y YAM 2.8 en directorios separados."))
        (set #faq_and_bugtracker (cat "\n"
                                      "Si tiene alguna consulta concerniente a YAM puede realizarla en el forum de YAM:\n"
                                      "http://forum.yam.ch/\n"
                                      "\n"
                                      "Las preguntas más frecuentes son a su vez\ contestadas en el FAQ de YAM:\n"
                                      "http://faq.yam.ch/\n"
                                      "\n"
                                      "En caso de que encuentre algún fallo en YAM, por favor, hágalo saber en el registro de fallos de YAM:\n"
                                      "http://bugs.yam.ch/"))
        (set #done_1   (cat "\n"
                            "YAM ha sido instalado satisfactoriamente.\n"
                            "\n"
                            "Haga doble click en el icono de \"YAM\" en el \n"))
        (set #done_2   (cat "\n"
                            "directorio seleccionado.\n"
                            "\n"
                            "Que lo disfrute..."))
    )
)
; ///
;----------------------------------------------------------------------------
; /// Checking environment
(set EXEC_ver (getversion "exec.library" (resident)))
(set NEWLIB_ver (getversion "newlib.library" (resident)))
(set AROS_ver (getversion "aros.library" (resident)))

; we use newlib.library to check whether we are running on AmigaOS 4.x
(if (>= NEWLIB_ver (* 4 65536))
    (set detectedSystem 1)
    (
        ; we are not running on AmigaOS 4.x
        (if (>= AROS_ver (* 40 65536))
            (set detectedSystem 3)
            (
                ; we are not running on AROS
                ; now check if we are running on MorphOS (exec.library V50+) or on AmigaOS 3.x
                (if (>= EXEC_ver (* 50 65536))
                    (set detectedSystem 2)
                    (set detectedSystem 0)
                )
            )
        )
    )
)

(set detectedSystem
    (askchoice
        (prompt #acknowledgeOS)
        (help @askchoice-help)
        (choices "AmigaOS 3.x"
                 "AmigaOS 4.x"
                 "MorphOS"
                 "AROS"
        )
        (default detectedSystem)
    )
)
; finally convert the system number into a string for easier understanding
(set detectedSystem
    (select detectedSystem "os3" "os4" "mos" "aros")
)

(if (= detectedSystem "os4")
    ; AmigaOS 4.x uses english locale names only
    (
        (set #cat_name_german                "german")
        (set #cat_name_swedish               "swedish")
        (set #cat_name_czech                 "czech")
        (set #cat_name_italian               "italian")
        (set #cat_name_french                "french")
        (set #cat_name_greek                 "greek")
        (set #cat_name_dutch                 "dutch")
        (set #cat_name_polish                "polish")
        (set #cat_name_spanish               "spanish")
        (set #cat_name_turkish               "turkish")
        (set #cat_name_hungarian             "hungarian")
        (set #cat_name_english_british       "english-british")
    )
    ; AmigaOS 3.x and MorphOS use localized locale names
    (
        (set #cat_name_german                "deutsch")
        (set #cat_name_swedish               "svenska")
        (set #cat_name_czech                 "czech")
        (set #cat_name_italian               "italiano")
        (set #cat_name_french                "français")
        (set #cat_name_greek                 "greek")
        (set #cat_name_dutch                 "nederlands")
        (set #cat_name_polish                "polski")
        (set #cat_name_spanish               "español")
        (set #cat_name_turkish               "türkçe")
        (set #cat_name_hungarian             "magyar")
    )
)

; ///
; /// Let's ask user for anything
(complete 0)

(message #no_way_back_warning)

(complete 1)

(if (<> 0 (exists "YAM:" (noreq)))
    (set YAM_def_dir (getassign "YAM"))
    (set YAM_def_dir @default-dest)
)

(set YAM_dir
    (askdir
        (default YAM_def_dir)
        (prompt #YAM_dir)
        (help    @askdir-help)
    )
)

(complete 10)

(if (= detectedSystem "os4")
    ; AmigaOS 4.x can handle different accents of one language (i.e. british english)
    (set catalog_lang
        (askoptions
            (prompt #lang_catalog)
            (help @askoptions-help)
            (choices "Deutsch"
                     "Svenska"
                     "Czech"
                     "Italiano"
                     "Français"
                     "ÅëëçíéêÜ"
                     "Español"
                     "Türkçe"
                     "Magyar"
                     "English (british)"
            )
            (default #def_cat_lang)
        )
    )
    ; but AmigaOS 3.x, MorphOS and AROS can not
    (set catalog_lang
        (askoptions
            (prompt #lang_catalog)
            (help @askoptions-help)
            (choices "Deutsch"
                     "Svenska"
                     "Czech"
                     "Italiano"
                     "Français"
                     "ÅëëçíéêÜ"
                     "Español"
                     "Türkçe"
                     "Magyar"
            )
            (default #def_cat_lang)
        )
    )
)

; ///
; /// Here we go!
(complete 30)

; make sure the YAM dir really exists
(if (not (exists YAM_dir))
    (
        ; create the directory
        (makedir YAM_dir)
        ; copy the directory icon
        (copyfiles
            (source "//YAM 2.8.info")
            (dest (tackon YAM_dir "/"))
            (newname "YAM.info")
            (infos)
            (noposition)
            (help @copyfiles-help)
        )
    )
)

; copy the executable
(copyfiles
    (source "/YAM")
    (dest YAM_dir)
    (help @copyfiles-help)
)
(if (not (exists (tackon YAM_dir "YAM.info")))
    (copyfiles
        (source "/YAM.info")
        (dest YAM_dir)
        (infos)
        (noposition)
        (help @copyfiles-help)
    )
)

(complete 40)

; copy .taglines file
(if (not (exists (tackon YAM_dir ".taglines")))
    (copyfiles
        (source "/.taglines")
        (dest YAM_dir)
        (help @copyfiles-help)
    )
)

; copy .addressbook file
(if (not (exists (tackon YAM_dir ".addressbook")))
    (copyfiles
        (source "/.addressbook")
        (dest YAM_dir)
        (help @copyfiles-help)
    )
)

; check if there were any catalogs selected at all
(if (<> 0 catalog_lang)
    (
        ; make sure the Catalogs directory exists
        (set Cat_dir (tackon YAM_dir "Catalogs"))
        (if (not (exists Cat_dir))
            (makedir Cat_dir)
        )

        ; copy the icon of the Catalogs directory
        (if (not (exists (tackon YAM_dir "Catalogs.info")))
            (copyfiles
                (source "/Catalogs.info")
                (dest YAM_dir)
                (infos)
                (noposition)
                (help @copyfiles-help)
            )
        )

        ; copy the YAM.cd file for people wanting to translate
        (if (not (exists (tackon Cat_dir "YAM.cd")))
            (copyfiles
                (source "/Catalogs/YAM.cd")
                (dest Cat_dir)
                (infos)
                (noposition)
                (help @copyfiles-help)
            )
        )

        ; german catalog file
        (if (bitand catalog_lang #def_lang_german)
            (
                (makedir (tackon Cat_dir #cat_name_german))

                (copyfiles
                     (source "/Catalogs/german")
                     (choices "YAM.catalog")
                     (dest (tackon Cat_dir #cat_name_german))
                     (help @copyfiles-help)
                )
            )
        )

        ; swedish catalog file
        (if (bitand catalog_lang #def_lang_swedish)
            (
                (makedir (tackon Cat_dir #cat_name_swedish))

                (copyfiles
                    (source "/Catalogs/swedish")
                    (choices "YAM.catalog")
                    (dest (tackon Cat_dir #cat_name_swedish))
                    (help @copyfiles-help)
                )
            )
        )

        ; czech catalog file
        (if (bitand catalog_lang #def_lang_czech)
            (
                (makedir (tackon Cat_dir #cat_name_czech))

                (copyfiles
                    (source "/Catalogs/czech")
                    (choices "YAM.catalog")
                    (dest (tackon Cat_dir #cat_name_czech))
                    (help @copyfiles-help)
                )
            )
        )

        ; italian catalog file
        (if (bitand catalog_lang #def_lang_italian)
            (
                (makedir (tackon Cat_dir #cat_name_italian))

                (copyfiles
                    (source "/Catalogs/italian")
                    (choices "YAM.catalog")
                    (dest (tackon Cat_dir #cat_name_italian))
                    (help @copyfiles-help)
                )
            )
        )

        ; french catalog file
        (if (bitand catalog_lang #def_lang_french)
            (
                (makedir (tackon Cat_dir #cat_name_french))

                (copyfiles
                    (source "/Catalogs/french")
                    (choices "YAM.catalog")
                    (dest (tackon Cat_dir #cat_name_french))
                    (help @copyfiles-help)
                )
            )
        )

        ; greek catalog file
        (if (bitand catalog_lang #def_lang_greek)
            (
                (makedir (tackon Cat_dir #cat_name_greek))

                (copyfiles
                    (source "/Catalogs/greek")
                    (choices "YAM.catalog")
                    (dest (tackon Cat_dir #cat_name_greek))
                    (help @copyfiles-help)
                )
            )
        )

        ; dutch catalog file
        ;(if (bitand catalog_lang #def_lang_dutch)
        ;    (
        ;        (makedir (tackon Cat_dir #cat_name_dutch))

        ;        (copyfiles
        ;            (source "/Catalogs/dutch")
        ;            (choices "YAM.catalog")
        ;            (dest (tackon Cat_dir #cat_name_dutch))
        ;            (help @copyfiles-help)
        ;        )
        ;    )
        ;)

        ; polish catalog file
        ;(if (bitand catalog_lang #def_lang_polish)
        ;    (
        ;        (makedir (tackon Cat_dir #cat_name_polish))

        ;        (copyfiles
        ;            (source "/Catalogs/polish")
        ;            (choices "YAM.catalog")
        ;            (dest (tackon Cat_dir #cat_name_polish))
        ;            (help @copyfiles-help)
        ;        )
        ;    )
        ;)

        ; spanish catalog file
        (if (bitand catalog_lang #def_lang_spanish)
            (
                (makedir (tackon Cat_dir #cat_name_spanish))

                (copyfiles
                    (source "/Catalogs/spanish")
                    (choices "YAM.catalog")
                    (dest (tackon Cat_dir #cat_name_spanish))
                    (help @copyfiles-help)
                )
            )
        )

        ; turkish catalog file
        (if (bitand catalog_lang #def_lang_turkish)
            (
                (makedir (tackon Cat_dir #cat_name_turkish))

                (copyfiles
                    (source "/Catalogs/turkish")
                    (choices "YAM.catalog")
                    (dest (tackon Cat_dir #cat_name_turkish))
                    (help @copyfiles-help)
                )
            )
        )

        ; hungarian catalog file
        (if (bitand catalog_lang #def_lang_hungarian)
            (
                (makedir (tackon Cat_dir #cat_name_hungarian))

                (copyfiles
                    (source "/Catalogs/hungarian")
                    (choices "YAM.catalog")
                    (dest (tackon Cat_dir #cat_name_hungarian))
                    (help @copyfiles-help)
                )
            )
        )

        ; now the accented languages follow
        ; NOTE: only AmigaOS 4.x supports these!!
        (if (= detectedSystem "os4")
            ; english-british catalog file
            (if (bitand catalog_lang #def_lang_english_british)
                (
                    (makedir (tackon Cat_dir #cat_name_english_british))

                    (copyfiles
                        (source "/Catalogs/english-british")
                        (choices "YAM.catalog")
                        (dest (tackon Cat_dir #cat_name_english_british))
                        (help @copyfiles-help)
                    )
                )
            )
        )
     )
)

(complete 50)

(set Resources_dir (tackon YAM_dir "Resources"))
(if (not (exists Resources_dir))
    (makedir Resources_dir)
)
(if (not (exists (tackon YAM_dir "Resources.info")))
    (copyfiles
        (source "/Resources.info")
        (dest YAM_dir)
        (infos)
        (noposition)
        (help @copyfiles-help)
    )
)

(complete 55)

(set Themes_dir (tackon Resources_dir "Themes"))
(if (not (exists Themes_dir))
    (makedir Themes_dir)
)
(if (not (exists (tackon Resources_dir "Themes.info")))
    (copyfiles
        (source "/Resources/Themes.info")
        (dest Resources_dir)
        (infos)
        (noposition)
        (help @copyfiles-help)
    )
)
(copyfiles
    (source "/Resources/Themes/")
    (dest Themes_dir)
    (all)
    (help @copyfiles-help)
)

(complete 60)

(set Certificates_dir (tackon Resources_dir "Certificates"))
(if (not (exists Certificates_dir))
    (makedir Certificates_dir)
)
(copyfiles
    (source "/Resources/Certificates/")
    (dest Certificates_dir)
    (all)
    (help @copyfiles-help)
)

(complete 65)

(set Spamfilters_dir (tackon Resources_dir "Spamfilters"))
(if (not (exists Spamfilters_dir))
    (makedir Spamfilters_dir)
)
(copyfiles
    (source "/Resources/Spamfilters/")
    (dest Spamfilters_dir)
    (all)
    (help @copyfiles-help)
)

(complete 70)

(set Gallery_dir (tackon Resources_dir "Gallery"))
(if (not (exists Gallery_dir))
    (makedir Gallery_dir)
)
(if (not (exists (tackon Resources_dir "Gallery.info")))
    (copyfiles
        (source "/Resources/Gallery.info")
        (dest Resources_dir)
        (infos)
        (noposition)
        (help @copyfiles-help)
    )
)
(copyfiles
    (source "/Resources/Gallery/")
    (dest Gallery_dir)
    (all)
    (help @copyfiles-help)
)

(complete 75)

(set Docs_dir (tackon YAM_dir "Docs"))
(if (not (exists Docs_dir))
    (makedir Docs_dir)
)
(copyfiles
    (source "/Docs/")
    (dest Docs_dir)
    (all)
)

(if (not (exists (tackon YAM_dir "Docs.info")))
    (copyfiles
        (source "/Docs.info")
        (dest YAM_dir)
        (infos)
        (noposition)
        (help @copyfiles-help)
    )
)

; copy the ReadMe file
(copyfiles
    (source "/ReadMe")
    (dest YAM_dir)
    (infos)
    (help @copyfiles-help)
)

(complete 80)

(set ARexx_dir (tackon YAM_dir "Rexx"))
(if (not (exists ARexx_dir))
    (makedir ARexx_dir)
)
(copyfiles
    (source "/Rexx/")
    (dest ARexx_dir)
    (all)
    (help @copyfiles-help)
)

(if (not (exists (tackon YAM_dir "Rexx.info")))
    (copyfiles
        (source "/Rexx.info")
        (dest YAM_dir)
        (infos)
        (noposition)
        (help @copyfiles-help)
    )
)

(complete 85)

(if (= detectedSystem "mos")
    ; MorphOS has no MUI: assign anymore but places MUI subclasses in SYS:Classes/MUI
    (set #MCC_dir "SYS:Classes/MUI/")
    (
        (if (= detectedSystem "aros")
            ; AROS has no MUI: assign anymore but places MUI subclasses in SYS:Classes/Zune
            (set #MCC_dir "SYS:Classes/Zune/")
            ; AmigaOS 3.x and AmigaOS 4.x use the MUI: assign
            (set #MCC_dir "MUI:libs/mui/")
        )
    )
)

(foreach "MUI/" "#?.mc?"
    (copylib
        (prompt (cat "MUI - " @each-name))
        (source (cat "MUI/" @each-name))
        (dest #MCC_dir)
        (confirm)
        (help @copylib-help)
    )
)

(complete 90)

(foreach "Libs/" "#?.library"
    (copylib
        (prompt @each-name)
        (source (cat "Libs/" @each-name))
        (dest "LIBS:")
        (confirm)
        (help @copylib-help)
    )
)

; copy the charset files for AmigaOS3, MorphOS and AROS only,
; and only if codesets.library is installed already
(if (or (or (= detectedSystem "os3") (= detectedSystem "mos")) (= detectedSystem "aros"))
    (if (exists "LIBS:codesets.library" (noreq))
        (copyfiles
            (prompt #ask_charsets)
            (source "Libs/Charsets/")
            (dest "LIBS:Charsets")
            (all)
            (confirm)
            (help @copyfiles-help)
        )
    )
)

(complete 99)

(message #faq_and_bugtracker)

(complete 100)

(message #done_1 "\"" YAM_dir "\"" #done_2)

; /// ********************
;----------------------------------------------------------------------------
(exit (QUIET))
